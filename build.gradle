plugins {
  id 'war'
  id 'eclipse-wtp'
  id 'jacoco'
  id 'checkstyle'
  id 'pmd'
  id "com.github.spotbugs" version "4.5.0"
  id "com.bmuschko.cargo" version "2.7.1"
  id "org.flywaydb.flyway" version "6.5.5"

}

group = 'sample.webapp'
version = '0.0.1-SNAPSHOT'

description = """JSFによるWebアプリケーションのサンプル"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Javaの文字コード
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {

     mavenCentral()
     maven { url "https://plugins.gradle.org/m2/" }
     maven { url "https://repository.jboss.org/nexus/content/repositories/releases/" }
     maven { url "https://repository.primefaces.org/" }
}

dependencies {

    // JavaEE
    providedCompile group: 'javax', name: 'javaee-web-api', version: '8.0.1'

    // JavaEE（Java9以降で標準ではなくなったため、追加で指定が必要）
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1' //
    compile group: 'javax.jws', name: 'javax.jws-api', version: '1.1'
    compile group: 'javax.xml.ws', name: 'jaxws-api', version: '2.3.1'

    // フレームワーク
    compile group: 'org.primefaces', name: 'primefaces', version: '8.0'
    compile group: 'org.primefaces.themes', name: 'all-themes', version: '1.0.10'
    compile group: 'org.omnifaces', name: 'omnifaces', version: '3.4.1'
    compile group: 'javax.security.enterprise', name: 'javax.security.enterprise-api', version: '1.0'
    //compile group: 'org.glassfish.soteria', name: 'javax.security.enterprise', version: '1.0'

    // ログ
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.30'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.13.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'

    // JDBC
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.12', changing: true
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'

    // DBアクセス
    compile group: 'org.mybatis', name: 'mybatis', version: '3.4.6'
    compile group: 'org.mybatis', name: 'mybatis-cdi', version: '1.1.1'
    compile group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.4.0'
    compile group: 'org.eclipse.persistence', name: 'org.eclipse.persistence.jpa', version: '2.7.6'
    // compile group: 'com.github.gwenn', name: 'sqlite-dialect', version: '0.1.0' // SQLiteでHibanateによるJPAをする際、必要

    // ユーティリティ
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    compile group: 'commons-fileupload', name: 'commons-fileupload', version: '1.4' // PrimeFacesのファイルアップロードで必要
    compile group: 'commons-codec', name: 'commons-codec', version: '1.14'

    compile group: 'jp.fintan.keel', name: 'keel-transaction-token-check', version: '1.0.0'

    // テスト
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.6.2'
    testCompile group: 'org.dbunit', name: 'dbunit', version: '2.7.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
    testCompile group: 'com.codeborne', name: 'selenide', version: '5.11.1'

     // GradleでLombokをビルドするためには、以下のようにする
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "1.18.12"

    def cargoVersion = '1.7.10'
    cargo "org.codehaus.cargo:cargo-core-uberjar:${cargoVersion}",
          "org.codehaus.cargo:cargo-ant:${cargoVersion}"
}

eclipse {

    wtp {
        component {
            contextPath  = contextPath
        }
    }
}

cargo {

    containerId = cargoContainerId
    port = Integer.parseInt(cargoPort)

    deployable {
        // ページを表示するためのURL：http://{ホスト名}/contextPath/{ページ名}
        //   例：http://localhost:8080/SampleWebApp/login.xhtml
        context = contextPath
    }

    local {
        // 開始タスク：clean cargoStartLocal
        // 終了タスク：cargoStopLocal
        homeDir = file(cargoLocalApServerHome)
        startStopTimeout = 60000
    }

    remote {
        // タスク：clean cargoRedeployRemote
        hostname = cargoHostName
        username = cargoHostUser
        password = cargoHostPassword
    }

}

// Flyway管理テーブルを作成するため、最初にflywayBaselineタスクを実行する
flyway {
    url = 'jdbc:postgresql://localhost:5432/postgres'
    user = 'postgres'
    password = 'postgres'
    schemas = ['public']
    locations = [ "filesystem:${projectDir}/db/migration" ] // デフォルトはsrc/main/db/migration
}

checkstyle {
    toolVersion = '8.36' // Gradleラッパーの場合、バージョン指定がないとエラーになる
    configFile = file('misc/checkstyle/google_checks.xml')
    sourceSets = [sourceSets.main]
    ignoreFailures = true
}

spotbugs {
    ignoreFailures = true
}

pmd {
    sourceSets = [sourceSets.main]
    ignoreFailures = true
}

test.finalizedBy jacocoTestReport

// 自動テストタスク：clean flywayMigrate cargoStartLocal check cargoStopLocal

