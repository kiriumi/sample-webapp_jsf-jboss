plugins {
  id 'war'
  id 'maven'
  id 'eclipse-wtp'
  id "com.bmuschko.cargo" version "2.4"
}

group = 'sample.webapp'
version = '0.0.1-SNAPSHOT'

description = """sample-webapp"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Javaの文字コード
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {

     mavenCentral()
     maven { url "https://repository.jboss.org/nexus/content/repositories/releases/" }
}

dependencies {

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'

    testCompile group: 'junit', name: 'junit', version:'3.8.1'

    providedCompile fileTree(dir: "$serverHome/modules/system/layers/base", include: '**/*.jar')

    def cargoVersion = '1.4.5'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
          "org.codehaus.cargo:cargo-ant:$cargoVersion"
}

eclipse {

    wtp {
        component {
            contextPath  = contextPath
        }
    }
}

cargo {

    containerId = cargoContainerId
    port = Integer.parseInt(cargoPort)

    deployable {
        // ページを表示するためのURL：ホスト名/contextPath/ページ名
        //   例：localhost:8080/SampleWebApp/home.xhtml
        context = contextPath
    }

    local {
        // タスク：cargoStopLocal clean assemble cargoStartLocal
        homeDir = file(cargoLocalServerDir) // プロジェクト内にサーバがあることを想定
        timeout = 60000
    }

    remote {
        // タスク：clean assemble cargoUndeployRemote cargoDeployRemote
        hostname = cargoHostName
        username = cargoHostUser
        password = cargoHostPassword
    }

}
