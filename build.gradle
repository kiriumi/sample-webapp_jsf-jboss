plugins {
  id 'war'
  id 'maven'
  id 'eclipse-wtp'
  id "com.bmuschko.cargo" version "2.4"
}

group = 'sample.webapp'
version = '0.0.1-SNAPSHOT'

description = """sample-webapp"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Javaの文字コード
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {

     mavenCentral()
     maven { url "https://repository.jboss.org/nexus/content/repositories/releases/" }
}

dependencies {

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'org.mybatis', name: 'mybatis', version: '3.4.6'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'

    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
    compile group: 'com.github.gwenn', name: 'sqlite-dialect', version: '0.1.0'

    compile group: 'org.primefaces', name: 'primefaces', version: '7.0'
    compile group: 'org.omnifaces', name: 'omnifaces', version: '3.4.1'

    testCompile group: 'junit', name: 'junit', version:'3.8.1'

    providedCompile group: 'javax', name: 'javaee-api', version: '8.0.1'
    providedCompile group: 'org.projectlombok', name: 'lombok', version: '1.18.12'

    def cargoVersion = '1.4.5'
    cargo "org.codehaus.cargo:cargo-core-uberjar:${cargoVersion}",
          "org.codehaus.cargo:cargo-ant:${cargoVersion}"
}

eclipse {

    wtp {
        component {
            contextPath  = contextPath
        }
    }
}

cargo {

    containerId = cargoContainerId
    port = Integer.parseInt(cargoPort)

    deployable {
        // ページを表示するためのURL：http://{ホスト名}/contextPath/{ページ名}
        //   例：http://localhost:8080/SampleWebApp/login.xhtml
        context = contextPath
    }

    local {
        // タスク：clean assemble cargoStopLocal cargoStartLocal
        homeDir = file(cargoLocalServerDir) // プロジェクト内にサーバがあることを想定
        timeout = 60000
    }

    remote {
        // タスク：clean assemble cargoUndeployRemote cargoDeployRemote
        hostname = cargoHostName
        username = cargoHostUser
        password = cargoHostPassword
    }

}
